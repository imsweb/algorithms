plugins {
    id 'java-library'
    id 'checkstyle'
    id 'com.github.spotbugs' version '5.0.3'
    id 'maven-publish'
    id 'signing'
    id 'io.github.gradle-nexus.publish-plugin' version '1.1.0'
    id 'org.sonatype.gradle.plugins.scan' version '2.2.2'
    id "com.github.ben-manes.versions" version '0.38.0'
}

group = 'com.imsweb'
version = file('VERSION').text
description = 'Java implementation of cancer-related algorithms (NHIA, NAPIIA, Survival Time, etc...)'
sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

println "Starting build using JDK ${Runtime.version().feature()}"

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'com.opencsv:opencsv:5.5.2'
    implementation 'com.thoughtworks.xstream:xstream:1.4.18'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'com.imsweb:layout:4.4'
    testImplementation 'com.imsweb:seerutils:5.6'
    testImplementation 'com.imsweb:seerutils-gui:1.18'
}

// enforce UTF-8, display the compilation warnings
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.compilerArgs << '-Xlint:unchecked' << '-Xlint:deprecation'
}

// the Javadoc was made way too strict in Java 8 and it's not worth the time fixing everything!
tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
}

// generate javadoc and sources (required by Nexus)
java {
    withJavadocJar()
    withSourcesJar()
}

// customize the manifest
jar {
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Implementation-Vendor': 'Information Management Services Inc.',
                'Created-By': System.properties['java.vm.version'] + ' (' + System.properties['java.vm.vendor'] + ')',
                'Built-By': System.getProperty('user.name'),
                'Built-Date': new Date(),
                'Built-JDK': System.getProperty('java.version'),
                'Automatic-Module-Name': 'com.imsweb.algorithms'
        )
    }
}

// Nexus vulnerability scan (https://github.com/sonatype-nexus-community/scan-gradle-plugin)
ossIndexAudit {
    dependencyGraph = true
    printBanner = false

    // only set proxy if running outside of Github Actions
    if (!System.getenv().GITHUB_ACTIONS) {
        proxyConfiguration {
            protocol = 'http'
            host = 'webproxy-btp.imsweb.com'
            port = 8080
        }
    }
}
check.dependsOn 'ossIndexAudit'

// checkstyle plugin settings
checkstyle {
    ignoreFailures = true
    configFile = project(':').file('config/checkstyle/checkstyle.xml')
    configProperties = ['suppressionFile': project(':').file('config/checkstyle/checkstlye-exclude.xml')]
}

// spotbugs plugin settings
spotbugs {
    ignoreFailures = true
    excludeFilter = project(':').file("config/spotbugs/spotbugs-exclude.xml")
}

// needed to deploy to Maven Central
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'algorithms'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'Algorithms'
                description = 'Java implementation of cancer-related algorithms (NHIA, NAPIIA, Survival Time, etc...)'
                url = 'https://github.com/imsweb/algorithms'
                inceptionYear = '2015'

                licenses {
                    license {
                        name = 'A modified BSD License (BSD)'
                        url = 'https://github.com/imsweb/algorithms/blob/master/LICENSE'
                        distribution = 'repo'
                    }
                }

                developers {
                    developer {
                        id = 'depryf'
                        name = 'Fabian Depry'
                        email = 'depryf@imsweb.com'
                    }
                }

                scm {
                    url = 'https://github.com/imsweb/algorithms'
                    connection = 'scm:https://github.com/imsweb/algorithms.git'
                    developerConnection = 'scm:git@github.com:imsweb/algorithms.git'
                }
            }
        }
    }
}

// setup JAR signing
signing {
    def signingKey = project.findProperty('signing.armored.key') ?: ''
    def signingPassword = project.findProperty('signing.armored.password') ?: ''

    useInMemoryPgpKeys(signingKey, signingPassword)

    sign publishing.publications.mavenJava
}

// needed to release on maven central
nexusPublishing {
    repositories {
        sonatype {
            stagingProfileId = 'ImsAlgorithmsProfileId'

            username = project.findProperty("nexusUsername")
            password = project.findProperty("nexusPassword")
        }
    }

    clientTimeout = java.time.Duration.ofSeconds(300)
    connectTimeout = java.time.Duration.ofSeconds(60)

    transitionCheckOptions {
        maxRetries.set(50)
        delayBetween.set(java.time.Duration.ofMillis(5000))
    }
}

// don't try to release a snapshot to a non-snapshot repository, that won't work anyway
if (version.endsWith('-SNAPSHOT')) {
    gradle.startParameter.excludedTaskNames += 'signMavenJavaPublication'
    gradle.startParameter.excludedTaskNames += 'closeAndReleaseSonatypeStagingRepository'
}

// Gradle wrapper, this allows to build the project without having to install Gradle!
wrapper {
    gradleVersion = '7.3.3'
    distributionType = Wrapper.DistributionType.ALL
}


