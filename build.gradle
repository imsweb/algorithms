import java.time.Duration

plugins {
    id 'java-library'
    id 'checkstyle'
    id 'jacoco'
    id 'com.github.spotbugs' version '6.0.26'
    id 'maven-publish'
    id 'signing'
    id 'io.github.gradle-nexus.publish-plugin' version '2.0.0' // publish to Maven Central
    id 'com.github.ben-manes.versions' version '0.51.0' // check for out-of-date dependencies (run 'dependencyUpdates' manually)
    id 'org.sonatype.gradle.plugins.scan' version '2.8.3' // scan for vulnerabilities
    id "org.sonarqube" version "5.1.0.4882"// sonarQube analysis
}

group = 'com.imsweb'
version = file('VERSION').text
description = 'Java implementation of cancer-related algorithms (NHIA, NAPIIA, Survival Time, etc...)'

println "Starting build using JDK ${Runtime.version().feature()}"

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.thoughtworks.xstream:xstream:1.4.21'
    implementation 'org.apache.commons:commons-lang3:3.17.0'
    implementation 'com.opencsv:opencsv:5.9'

    testImplementation 'com.imsweb:layout:6.0'
    testImplementation 'com.imsweb:seerutils:5.6'
    testImplementation 'com.imsweb:seerutils-gui:1.20'
    testImplementation 'junit:junit:4.13.2'
}

// enforce UTF-8, display the compilation warnings
tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.compilerArgs << '-Xlint:unchecked' << '-Xlint:deprecation'
}

// the Javadoc was made way too strict in Java 8 and it's not worth the time fixing everything!
tasks.withType(Javadoc).configureEach {
    options.addStringOption('Xdoclint:none', '-quiet')
}

// generate javadoc and sources (required by Nexus)
java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    withJavadocJar()
    withSourcesJar()
}

// customize the manifest
jar {
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Implementation-Vendor': 'Information Management Services Inc.',
                'Created-By': System.properties['java.vm.version'] + ' (' + System.properties['java.vm.vendor'] + ')',
                'Built-By': System.getProperty('user.name'),
                'Built-Date': new Date(),
                'Built-JDK': System.getProperty('java.version'),
                'Automatic-Module-Name': 'com.imsweb.algorithms'
        )
    }
}

// Nexus vulnerability scan (https://github.com/sonatype-nexus-community/scan-gradle-plugin)
ossIndexAudit {
    outputFormat = 'DEPENDENCY_GRAPH'
    printBanner = false
}
check.dependsOn 'ossIndexAudit'

// checkstyle plugin settings
checkstyle {
    ignoreFailures = false
    configFile = project(':').file('config/checkstyle/checkstyle.xml')
    configProperties = ['suppressionFile': project(':').file('config/checkstyle/checkstlye-exclude.xml')]
}

// jacoco plugin settings
jacocoTestReport {
    reports {
        xml.required = true
    }
}
test.finalizedBy jacocoTestReport

// spotbugs plugin settings
spotbugs {
    ignoreFailures = false
    excludeFilter.set(project(':').file("config/spotbugs/spotbugs-exclude.xml"))
}

sonarqube {
    properties {
        property "sonar.projectKey", "imsweb_algorithms"
        property "sonar.organization", "imsweb"
        property "sonar.host.url", "https://sonarcloud.io"
        property 'sonar.exclusions', '**/lab/*'
        property 'sonar.coverage.exclusions', '**/lab/*'
    }
}

// needed to deploy to Maven Central
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'algorithms'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'Algorithms'
                description = 'Java implementation of cancer-related algorithms (NHIA, NAPIIA, Survival Time, etc...)'
                url = 'https://github.com/imsweb/algorithms'
                inceptionYear = '2015'

                licenses {
                    license {
                        name = 'A modified BSD License (BSD)'
                        url = 'https://github.com/imsweb/algorithms/blob/master/LICENSE'
                        distribution = 'repo'
                    }
                }

                developers {
                    developer {
                        id = 'depryf'
                        name = 'Fabian Depry'
                        email = 'depryf@imsweb.com'
                    }
                }

                scm {
                    url = 'https://github.com/imsweb/algorithms'
                    connection = 'scm:https://github.com/imsweb/algorithms.git'
                    developerConnection = 'scm:git@github.com:imsweb/algorithms.git'
                }
            }
        }
    }
}

def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}

// https://github.com/ben-manes/gradle-versions-plugin
tasks.named("dependencyUpdates").configure {
    rejectVersionIf {
        isNonStable(it.candidate.version)
    }
}

// setup JAR signing
signing {
    required { !project.version.endsWith('-SNAPSHOT') }

    String signingKey = project.findProperty('signingKey') ?: ''
    String signingPassword = project.findProperty('signingPassword') ?: ''

    useInMemoryPgpKeys(signingKey, signingPassword)

    sign publishing.publications.mavenJava
}

// needed to release on maven central
nexusPublishing {
    repositories {
        sonatype {
            stagingProfileId = '63e5ddd3ab0d16'

            username = project.findProperty("nexusUsername")
            password = project.findProperty("nexusPassword")
        }
    }

    clientTimeout = Duration.ofSeconds(300)
    connectTimeout = Duration.ofSeconds(60)

    transitionCheckOptions {
        maxRetries.set(50)
        delayBetween.set(Duration.ofMillis(5000))
    }
}


// Gradle wrapper, this allows to build the project without having to install Gradle!
wrapper {
    gradleVersion = '8.11'
    distributionType = Wrapper.DistributionType.ALL
}


