plugins {
    id 'java-library'
    id 'checkstyle'
    id 'com.github.spotbugs' version '3.0.0'
    id 'com.bmuschko.nexus' version '2.3.1'
    id 'io.codearte.nexus-staging' version '0.21.2'
}

group = 'com.imsweb'
version = file('VERSION').text
description = 'Java implementation of cancer-related algorithms (NHIA, NAPIIA, Survival Time, etc...)'
sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

println "Starting build using JDK ${Runtime.version().feature()}"

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.apache.commons:commons-lang3:3.9'
    implementation 'com.opencsv:opencsv:5.0'
    implementation 'com.thoughtworks.xstream:xstream:1.4.11.1'

    testImplementation 'junit:junit:4.13'
    testImplementation 'com.imsweb:layout:1.24'
    testImplementation 'com.imsweb:seerutils:5.6'
    testImplementation 'com.imsweb:seerutils-gui:1.15'
}

// enforce UTF-8, display the compilation warnings
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.compilerArgs << '-Xlint:unchecked' << '-Xlint:deprecation'
}

// the Javadoc was made way too strict in Java 8 and it's not worth the time fixing everything!
tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
}

// customize the manifest
jar {
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Implementation-Vendor': 'Information Management Services Inc.',
                'Created-By': System.properties['java.vm.version'] + ' (' + System.properties['java.vm.vendor'] + ')',
                'Built-By': System.getProperty('user.name'),
                'Built-Date': new Date(),
                'Built-JDK': System.getProperty('java.version'),
                'Automatic-Module-Name': 'com.imsweb.algorithms'
        )
    }
}

// checkstyle plugin settings
checkstyle {
    ignoreFailures = true
    configFile = project(':').file('config/checkstyle/checkstyle.xml')
    configProperties = ['suppressionFile': project(':').file('config/checkstyle/checkstlye-exclude.xml')]
}

// spotbugs plugin settings
spotbugs {
    ignoreFailures = true
    excludeFilter = project(':').file("config/spotbugs/spotbugs-exclude.xml")
}

// needed to deploy to Maven Central
modifyPom {
    project {
        name 'Algorithms'
        description 'Java implementation of cancer-related algorithms (NHIA, NAPIIA, Survival Time, etc...)'
        url 'https://github.com/imsweb/algorithms'
        inceptionYear '2015'

        scm {
            url 'https://github.com/imsweb/algorithms'
            connection 'scm:https://github.com/imsweb/algorithms.git'
            developerConnection 'scm:git@github.com:imsweb/algorithms.git'
        }

        licenses {
            license {
                name 'A modified BSD License (BSD)'
                url 'https://github.com/imsweb/algorithms/blob/master/LICENSE'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'depryf'
                name 'Fabian Depry'
                email 'depryf@imsweb.com'
            }
        }
    }
}

// don't try to release a snapshot to a non-snapshot repository, that won't work anyway
if (version.endsWith('-SNAPSHOT'))
    gradle.startParameter.excludedTaskNames += 'closeAndReleaseRepository'

// Gradle wrapper, this allows to build the project without having to install Gradle!
wrapper {
    gradleVersion = '6.0.1'
    distributionType = Wrapper.DistributionType.ALL
}


